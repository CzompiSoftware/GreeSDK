plugins {
    id 'java'
    id 'maven-publish'
}

group = 'hu.czsoft'
version = rootProject.version

repositories {
    mavenCentral()
    maven {
        name = 'czsoft'
        url = "${rootProject.mavenUrl}releases"
    }
}
dependencies {
    testImplementation platform("org.junit:junit-bom:${rootProject.junitVersion}")
    testImplementation "org.junit.jupiter:junit-jupiter:${rootProject.junitVersion}"
    testRuntimeOnly "org.junit.platform:junit-platform-launcher:${rootProject.junitPlatformVersion}"

    compileOnly "org.projectlombok:lombok:${rootProject.lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${rootProject.lombokVersion}"

    implementation "com.google.code.gson:gson:${rootProject.gsonVersion}"
}
publishing {
    repositories {
        maven {
            name = 'czsoft'
            url = rootProject.mavenUrl + (!rootProject.version.endsWith('SNAPSHOT') ? "releases" : "snapshots")
            credentials(PasswordCredentials)
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            artifactId = rootProject.artifactId
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = rootProject.name
                description = "An SDK to easily communicate with your Gree appliances, written in Java. Originally separated from GreeControlAndroid, and then got a full rewrite."
                url = "https://docs.czsoft.hu/en/java/greesdk?v=" + rootProject.version
                developers {
                    developer {
                        id = "czompi"
                        name = "Dávid Czompó"
                        email = "czompi@czompi.eu"
                    }
                    developer {
                        id = "czompisoftware"
                        name = "Czompi Software"
                        email = "developer@czsoft.hu"
                    }
                }
                scm {
                    connection = "scm:git:git://github.com/CzompiSoftware/" + rootProject.artifactId + ".Java.git"
                    developerConnection = "scm:git:ssh://github.com/CzompiSoftware/" + rootProject.artifactId + ".Java.git"
                    url = "https://github.com/CzompiSoftware/" + rootProject.artifactId + ".Java"
                }
            }
        }
    }
}