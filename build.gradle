plugins {
    id 'java'
    id 'maven-publish'
}

group = 'eu.czsoft'
version = rootProject.version

repositories {
    mavenCentral()
    maven {
        name = 'czsoft'
        url = rootProject.mavenUrl + "releases"
    }
}
dependencies {
    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'

    compileOnly 'org.projectlombok:lombok:1.18.28'
    annotationProcessor 'org.projectlombok:lombok:1.18.28'

    implementation 'com.google.code.gson:gson:2.10.1'
}
publishing {
    repositories {
        maven {
            name = 'czsoft'
            url = rootProject.mavenUrl + (!rootProject.version.endsWith('SNAPSHOT') ? "releases" : "snapshots")
            credentials(PasswordCredentials)
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            artifactId = rootProject.artifactId
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = rootProject.name
                description = "An SDK to easily communicate with your Gree appliances. Separated from GreeControlAndroid."
                url = "https://docs.czsoft.eu/greesdk?v=" + rootProject.version
                developers {
                    developer {
                        id = "czompi"
                        name = "Dávid Czompó"
                        email = "czompi@czompi.eu"
                    }
                    developer {
                        id = "czompisoftware"
                        name = "Czompi Software"
                        email = "dev@czsoft.eu"
                    }
                }
                scm {
                    connection = "scm:git:git://github.com/CzompiSoftware/" + rootProject.artifactId + ".git"
                    developerConnection = "scm:git:ssh://github.com/CzompiSoftware/" + rootProject.artifactId + ".git"
                    url = "https://github.com/CzompiSoftware/" + rootProject.artifactId
                }
            }
        }
    }
}